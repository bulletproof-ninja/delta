plugins {
    id 'com.adarshr.test-logger' version '2.1.1'
    id 'com.github.maiflai.scalatest' version '0.29'
}

ext {
    getScalaFull = [
        '2.12': '2.12.15',
        '2.13': '2.13.8'
    ]

    vScala = System.properties.scalaVersion ?: '2.13'
    vScalaFull = getScalaFull[vScala]
    vScuff = '9.0.0-SNAPSHOT' //vScala == '2.13' ? 'SNAPSHOT-b2105241501' : 'SNAPSHOT-b2105241459'
    vScalaTest = '3.2.2'
}

allprojects {
    repositories {
        // jcenter()
        mavenCentral()
        maven {
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenLocal()
        jcenter()
    }

    apply plugin: 'scala'
    apply plugin: 'maven-publish'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.github.maiflai.scalatest'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version = rootProject.version

    configurations {
	    scalacPlugin {
	        transitive = false
	    }
    }

    dependencies {
        compile "org.scala-lang:scala-reflect:$vScalaFull"
        compile "org.scala-lang.modules:scala-collection-compat_$vScala:2.7.0"
        testCompile "org.scala-lang:scala-reflect:$vScalaFull"

        testCompile "junit:junit:4.12"
        testCompile "org.slf4j:slf4j-simple:1.7.26"
        testCompile "org.scalatest:scalatest_2.13:3.2.11"
        testCompile "com.vladsch.flexmark:flexmark-all:0.62.2"
        testCompile "io.scalaland:chimney_$vScala:0.6.1"

    }

    tasks.withType(Jar) {
        baseName = "${project.name}_$vScala"
    }

    tasks.withType(ScalaCompile) {
        def sharedDefault = "-feature -deprecation -explaintypes -unchecked -Xcheckinit -Xlint -language:implicitConversions,existentials,higherKinds"
        def args = System.properties.compilerArgs ?:
            vScala == "2.13"
            ? "$sharedDefault -Wunused"
            : "$sharedDefault -Ywarn-unused"
	    scalaCompileOptions.additionalParameters = args.split(" ").toList()
    }

    jar {
        version = properties.version ?: "0.0.1-SNAPSHOT"
        manifest {
            attributes "Implementation-Title": project.name, "Implementation-Version": version
        }
    }

    task sourceJar(type: Jar) {
        classifier "sources"
        from project.sourceSets.main.allScala
    }
    task docsJar(type: Jar, dependsOn: scaladoc) {
        classifier "docs"
        from project.scaladoc.destinationDir
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId System.properties.mavenGroupId ?: ""
                artifactId "${project.name}_$vScala"

                from components.java
                artifact sourceJar
                artifact docsJar
            }
        }
    }

    test {
        maxParallelForks = 1
        systemProperties System.properties
        testLogging {
            exceptionFormat = 'full'
        }
    }

}

dependencies {
    compile "ninja.bulletproof:scuff_$vScala:$vScuff"
}

subprojects {
    dependencies {
        testCompile rootProject.sourceSets.test.output
        compile rootProject
    }
}

wrapper {
    gradleVersion = '6.9'
}

project(':delta-cassandra') {
    dependencies {
        compile "com.datastax.cassandra:cassandra-driver-core:3.8.0"
    }
}

project(':delta-jdbc') {
    dependencies {
        testCompile "com.h2database:h2:2.1.210"
        testCompile "org.apache.derby:derby:10.12.1.1"
        testCompile "mysql:mysql-connector-java:8.0.23"
        testCompile "org.postgresql:postgresql:42.2.12"
        testCompile "com.microsoft.sqlserver:mssql-jdbc:7.2.2.jre8"
    }
}

project(':delta-hazelcast') {
    def vHz = '3.12.12'
    dependencies {
        compile "com.hazelcast:hazelcast:$vHz"
        testCompile "com.hazelcast:hazelcast-client:$vHz"
        testCompile "com.hazelcast:hazelcast-scala_$vScala:3.12.1"
    }
}

project(':delta-redis') {
    dependencies {
        compile "redis.clients:jedis:3.2.0"
    }
}

project(':delta-java') {
    dependencies {
        testCompile project(":delta-jdbc")
    }
}

project(':delta-mongodb') {
    dependencies {
        compile "org.mongodb:mongodb-driver-async:3.12.8"
        // compile "org.mongodb.scala:mongo-scala-bson_$vScala:2.8.0"
        // compile "org.mongodb:mongodb-driver-reactivestreams:1.11.0"
    }
}
