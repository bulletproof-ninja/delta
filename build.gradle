plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

ext {
    vScala = System.properties.scalaVersion ?: '2.11'
}

tasks.withType(ScalaCompile) {
    def args = System.properties.compilerArgs ?: "-feature -deprecation -explaintypes -unchecked -Xlint"
    scalaCompileOptions.additionalParameters = args.split(" ").toList()
}
tasks.withType(Jar) {
    baseName = "${project.name}_$vScala"
}

version = "0.9.0-SNAPSHOT"
def bintrayVersion = {
    if (version.endsWith('-SNAPSHOT')) {
        java.text.SimpleDateFormat format = new java.text.SimpleDateFormat('yyMMddHHmm')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')))
        return version.replace('SNAPSHOT', "b" + format.format(new Date()))
    } else {
        return version
    }
}()

allprojects {
    apply plugin: 'scala'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.7
    if (vScala == '2.11') {
        targetCompatibility = 1.7
    } else {
        targetCompatibility = 1.8
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "http://dl.bintray.com/nilskp/maven" 
        }
    }
    dependencies {
        compileOnly "org.scala-lang:scala-library:$vScala.8"
        compileOnly "org.scala-lang:scala-reflect:$vScala.8"
        testCompile "junit:junit:4.+"
        testCompile "org.mockito:mockito-all:1.10.+"
        testCompile "com.propensive:rapture-json-jackson_$vScala:2.0.+" 
    }

    jar {
        manifest {
            attributes "Implementation-Title": project.name, "Implementation-Version": project.version
        }
    }

	task sourceJar(type: Jar) {
	    classifier "sources"
	    from project.sourceSets.main.allScala
	}
	task docsJar(type: Jar, dependsOn: scaladoc) {
	    classifier "docs"
	    from project.scaladoc.destinationDir
	}

    publishing {
        publications {
            maven(MavenPublication) {
                groupId 'nilskp'
                artifactId jar.baseName
                version bintrayVersion

                from components.java
                artifact sourceJar
                artifact docsJar
            }
        }
    }
    bintray {
        user = System.properties.bintrayUser ?: "n/a"
        key = System.properties.bintrayApiKey ?: "n/a"
        dryRun = (user == "n/a" || key == "n/a")
        publish = true
        publications = ['maven']
        pkg {
            repo = "maven"
            name = "Ulysses"
            version {
                name = bintrayVersion
            }
        }
    }
}

dependencies {
    compile "nilskp:scuff_$vScala:0.1.5-b1701061638"
}

subprojects {
    version = rootProject.version
    dependencies {
        testCompile rootProject.sourceSets.test.output
        compile rootProject
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

project(':ulysses-cassandra') {
    dependencies {
        compile "com.datastax.cassandra:cassandra-driver-core:3.1.2"
    }
}
project(':ulysses-jdbc') {
    dependencies {
        testCompile "com.h2database:h2:1.4.+"
        testCompile "org.apache.derby:derby:10.12.+"
        testCompile "mysql:mysql-connector-java:5.1.40"
    }
}
project(':ulysses-mongodb') {
    dependencies {
        compile "org.mongodb:mongodb-driver-async:3.4.0"
    }
}
project(':ulysses-hazelcast') {
    dependencies {
        compile "com.hazelcast:hazelcast:3.7.4"
    }
}
